WHITESPACE   =  _{ " " | "\t" | NEWLINE }

name = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_" | "-")* }
file  =   { SOI ~ expr ~ EOI }
    expr     =   { prefix? ~ primary ~ postfix? ~ (infix ~ prefix? ~ primary ~ postfix? )* }
        infix    =  _{ add | sub | mul | div | pow }
            add    =   { "+" } // Addition
            sub    =   { "-" } // Subtraction
            mul    =   { "*" } // Multiplication
            div    =   { "/" } // Division
            pow    =   { "^" } // Exponentiation
        prefix   =  _{ neg }
            neg    =   { "-" } // Negation
        postfix  =  _{ fac }
            fac    =   { "!" } // Factorial
        primary  =  _{ number | func | "(" ~ expr ~ ")" | var }
            var     =  @{ name }
            func    =  {name ~ "(" ~ args ~ ")"}
                args    =  { expr ~ ("," ~ expr)* }
            number  =  ${ int ~ ( "." ~ fract ~ exp? )?}
                int    =  @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
                fract  =  @{ ASCII_DIGIT+ }
                exp    =  @{("e" | "E") ~ ("+" | "-")? ~ int}
